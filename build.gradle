apply plugin: 'java'
apply plugin: 'application'

group 'org.radarcns.restructurehdfs'
version '0.4.1-SNAPSHOT'
mainClassName = 'org.radarcns.hdfs.RestructureAvroRecords'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    avroVersion = '1.8.2'
    jacksonVersion = '2.9.6'
    hadoopVersion = '2.7.6'
    jCommanderVersion = '1.72'
}

repositories {
    jcenter()
}

dependencies {
    implementation group: 'org.apache.avro', name: 'avro', version: avroVersion
    implementation group: 'com.fasterxml.jackson.core' , name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.dataformat' , name: 'jackson-dataformat-csv', version: jacksonVersion
    implementation group: 'com.beust', name: 'jcommander', version: jCommanderVersion

    implementation group: 'org.apache.avro', name: 'avro-mapred', version: avroVersion
    implementation group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion

    runtimeOnly group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopVersion

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RADAR-base HDFS data restructuring',
                'Implementation-Version': version
    }
}

distributions {
    main {
        contents {
            into ("share/${project.name}") {
                from 'README.md', 'LICENSE'
            }
        }
    }
}

startScripts {
    classpath += files('lib/PlaceHolderForPluginPath')

    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('PlaceHolderForPluginPath', 'radar-hdfs-plugins\\*')
        unixScriptFile.text = unixScriptFile.text.replace('PlaceHolderForPluginPath', 'radar-hdfs-plugins/*')
    }
}

tasks.withType(Tar){
    compression = Compression.GZIP
    extension = 'tar.gz'
}

task downloadDependencies {
    description "Pre-downloads dependencies"
    configurations.compileClasspath.files
    configurations.runtimeClasspath.files
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath.files
    into "${buildDir}/third-party/"
}

wrapper {
    gradleVersion '4.8'
}
