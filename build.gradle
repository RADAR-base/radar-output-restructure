plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
    id "org.jetbrains.dokka"
    id 'com.avast.gradle.docker-compose' version "0.14.0"
}

group 'org.radarbase'
version '1.1.6'
mainClassName = 'org.radarbase.output.Application'

ext {
    moduleDescription = 'RADAR-base output restructuring'
    website = 'https://radar-base.org'
    githubRepoName = 'RADAR-base/Restructure-HDFS-topic'
    githubUrl = "https://github.com/${githubRepoName}"
    issueUrl = "${githubUrl}/issues"

    avroVersion = '1.10.1'
    jacksonVersion = '2.11.4'
    hadoopVersion = '3.3.0'
    jCommanderVersion = '1.78'
    almworksVersion = '1.1.2'
    junitVersion = '5.7.0'
    minioVersion = '8.0.3'
    jedisVersion = '3.4.0'
    slf4jVersion = '1.7.30'
    azureStorageVersion = '12.9.0'
}

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation, testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly, testRuntimeOnly
}

dependencies {
    api group: 'org.apache.avro', name: 'avro', version: avroVersion
    api group: 'com.fasterxml.jackson.core' , name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.dataformat' , name: 'jackson-dataformat-yaml', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.dataformat' , name: 'jackson-dataformat-csv', version: jacksonVersion
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    implementation "redis.clients:jedis:$jedisVersion"

    implementation group: 'com.beust', name: 'jcommander', version: jCommanderVersion
    implementation group: 'com.almworks.integers', name: 'integers', version: almworksVersion

    implementation "io.minio:minio:$minioVersion"
    implementation("com.azure:azure-storage-blob:$azureStorageVersion")
    implementation 'com.opencsv:opencsv:5.0'

    implementation group: 'org.apache.avro', name: 'avro-mapred', version: avroVersion
    implementation group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
    runtimeOnly group: 'org.apache.hadoop', name: 'hadoop-hdfs-client', version: hadoopVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    dokkaHtmlPlugin("org.jetbrains.dokka:kotlin-as-java-plugin:$kotlinVersion")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
        apiVersion = "1.4"
        languageVersion = "1.4"
    }
}

ext.sharedManifest = manifest {
    attributes(
            "Implementation-Title": rootProject.name,
            "Implementation-Version": version)
}

jar {
    manifest {
        from sharedManifest
        attributes 'Main-Class': mainClassName
    }
}

distributions {
    main {
        contents {
            into ("share/${project.name}") {
                from 'README.md', 'LICENSE'
            }
        }
    }
}

startScripts {
    classpath += files('lib/PlaceHolderForPluginPath')

    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('PlaceHolderForPluginPath', 'radar-output-plugins\\*')
        unixScriptFile.text = unixScriptFile.text.replace('PlaceHolderForPluginPath', 'radar-output-plugins/*')
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        setExceptionFormat("full")
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        showStandardStreams = true
        setExceptionFormat("full")
    }
    outputs.upToDateWhen { false }
    shouldRunAfter test
}

check.dependsOn(integrationTest)
dockerCompose.isRequiredBy(integrationTest)

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

task downloadDependencies {
    doLast {
        description "Pre-downloads dependencies"
        configurations.compileClasspath.files
        configurations.runtimeClasspath.files
    }
    outputs.upToDateWhen { false }
}

task copyDependencies(type: Copy) {
    from {
        configurations.runtimeClasspath.files
    }
    into "$buildDir/third-party/"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    manifest.from sharedManifest
}

task dokkaJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from("$buildDir/dokka/javadoc/")
}

publishing {
    publications {
        mavenJar(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact dokkaJar
            pom {
                url = githubUrl as String
                description = moduleDescription

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'blootsvoets'
                        name = 'Joris Borgdorff'
                        email = 'joris@thehyve.nl'
                        organization = 'The Hyve'
                    }

                }
                issueManagement {
                    system = 'GitHub'
                    url = issueUrl as String
                }
                organization {
                    name = 'RADAR-base'
                    url = website
                }
                scm {
                    connection = "scm:git:${githubUrl}" as String
                    url = githubUrl as String
                }
            }
        }
    }
}

bintray {
    user project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    override false
    publications 'mavenJar'
    pkg {
        repo = project.group
        name = rootProject.name
        userOrg = 'radar-base'
        desc = moduleDescription
        licenses = ['Apache-2.0']
        websiteUrl = website
        issueTrackerUrl = issueUrl
        vcsUrl = githubUrl
        githubRepo = githubRepoName
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = description
            vcsTag = System.getenv('TRAVIS_TAG')
            released = new Date()
        }
    }
}

wrapper {
    gradleVersion '6.7.1'
}
