apply plugin: 'java'
apply plugin: 'application'

group 'org.radarcns.restructurehdfs'
version '0.1-SNAPSHOT'
mainClassName = 'org.radarcns.RestructureAvroRecords'

run {
    args = ['webhdfs://radar-test.thehyve.net:50070', '/topicAndroidPhoneNew/android_phone_sensor_acceleration', "${projectDir}/data"]
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.avroVersion = '1.8.1'
ext.jacksonVersion = '2.8.5'
ext.hadoopVersion = '2.7.3'
ext.log4jVersion = '2.8.1'

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion
    compile group: 'org.apache.avro', name: 'avro', version: avroVersion
    compile group: 'org.apache.avro', name: 'avro-mapred', version: avroVersion
    compile group: 'com.fasterxml.jackson.core' , name: 'jackson-databind', version: jacksonVersion

    runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion
    runtime group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoopVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    dependsOn configurations.runtime
    manifest {
        attributes 'Implementation-Title': 'radar-restructure-hdfs',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = project.name + '-all'
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude 'META-INF/*'
    }
    with jar
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
    distributionUrl distributionUrl.replace("bin", "all")
}
